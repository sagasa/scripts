#戦車/航空機/戦略物資


#{facility.pointitem.[0|1]}	1研究ポイントのアイテム

#{facility.ID.list::*}
#{facility.[ID]::name}		[<text?]
#{facility.[ID]::lore}		[<text>]
#{facility.[ID]::type}		[army|airforce|factory]

#---------------
#   研究ポイント
#---------------
#{facility.[ID]::capacity.max}	最大値
#{facility.[ID]::capacity.rate}	回復量

#{facility.[ID]::output.max}	最大引き出し数
#{facility.[ID]::output.rate}	クールタイム

#---------------
#   アイテム交換
#---------------
#{facility.[ID]::item.output}	出力アイテム
#{facility.[ID]::item.request}	要求アイテム
#{facility.[ID]::output.rate}	クールタイム
#{facility.[ID]::ct.private}	個人クールタイムかどうか

#---------------
#   稼働関連
#---------------
#{facility.operation_all}		全体稼働
#{facility.operation_all::*} 	全体稼働に含まれる施設ID
#{facility.operation::*}		各施設の稼働状態

#{facility.operation_rotate_p}	ローテーションのポインタ


#========================
#  外部から実行してほしいコマンド  
#========================
#TCLから/facility start で稼働			戦争時間開始に
#TCLから/facility stop で停止			戦争時間終了に
#TCLから/facility time で時間経過の回復	1分おきに呼んで

#TCLから/facility rotate_next でローテーションを進める
#TCLから/facility rotate_stop で稼働中の施設を止める

#/facility transfer で振込GUI

command /facility [<player>] [<text>] [<number>] [<number>]:
	permission: console.console
	trigger:
		#プレイヤーを変数に
		if arg-1 is set:
			set {_player} to arg-1
		else:
			set {_player} to player

		#=============
		#    稼働
		#=============
		if arg-2 is "start":
			if arg-3 is not set:
				set {_n} to 0
				loop 45 times:
					if {facility.ID.list::%{_n}%} is true:
						if {facility.operation_all::%{_n}%} is set:
							execute console command "/facility %{_player}% start %{_n}%"
					add 1 to {_n}
			else:		
				broadcast "&6[Facility]&3%{facility.%arg-3%::name}%&dが稼働しました"
				if {facility.ID.list::%arg-3%} is true:
					set {facility.operation::%arg-3%} to true
					loop {facility.sign.%arg-3%::*}:
						set line 3 of loop-value to "[&a稼働中&0]"

		if arg-2 is "stop":
			if arg-3 is not set:
				set {_n} to 0
				loop 45 times:
					if {facility.ID.list::%{_n}%} is true:
						if {facility.operation_all::%{_n}%} is set:
							execute console command "/facility %{_player}% stop %{_n}%"
					add 1 to {_n}
			else:
				broadcast "&6[Facility]&3%{facility.%arg-3%::name}%&dが停止しました"
				if {facility.ID.list::%arg-3%} is true:
					clear {facility.operation::%arg-3%}
					loop {facility.sign.%arg-3%::*}:
						set line 3 of loop-value to "[&4停止中&0]"

		if arg-2 is "rotate_next":
			if {facility.operation_rotate_p} is not set:
				set {facility.operation_rotate_p} to 0
			add 1 to {facility.operation_rotate_p}
			if {facility.operation_rotate.%{facility.operation_rotate_p}%::*} is not set:
				set {_new} to 0
				set {_old} to {facility.operation_rotate_p} - 1
				set {facility.operation_rotate_p} to 0
			else:
				set {_new} to {facility.operation_rotate_p}
				set {_old} to {facility.operation_rotate_p} - 1
			#稼働処理
			loop {facility.operation_rotate.%{_old}%::*}:
				execute console command "/facility %{_player}% stop %loop-value%"
			loop {facility.operation_rotate.%{_old}%::*}:
				execute console command "/facility %{_player}% start %loop-value%"

		if arg-2 is "rotate_stop":
			if {facility.operation_rotate_p} is not set:
				set {facility.operation_rotate_p} to 0
			add 1 to {facility.operation_rotate_p}
			if {facility.operation_rotate.%{facility.operation_rotate_p}%::*} is not set:
				set {_new} to 0
				set {_old} to {facility.operation_rotate_p} - 1
				set {facility.operation_rotate_p} to 0
			else:
				set {_new} to {facility.operation_rotate_p}
				set {_old} to {facility.operation_rotate_p} - 1
			#稼働処理
			loop {facility.operation_rotate.%{_old}%::*}:
				execute console command "/facility %{_player}% stop %loop-value%"

		#===============
		# 研究ポイントの振込
		#===============
		if arg-2 is "transfer":
			if arg-3 is not set:
				wait 2 tick
				open chest with 1 row named "&d研究ポイントの振込" to {_player}
				wait 1 tick
				format slot 0 of {_player} with light green glass pane named "&a陸軍の研究ポイントを振込&6:1ポイント" with lore "" to run "facility %{_player}% transfer 1 0"
				format slot 1 of {_player} with light green glass pane named "&a陸軍の研究ポイントを振込&6:10ポイント" with lore "" to run "facility %{_player}% transfer 10 0"
				format slot 2 of {_player} with light green glass pane named "&a陸軍の研究ポイントを振込&6:全て" with lore "" to run "facility %{_player}% transfer -1 0"

				format slot 4 of {_player} with light blue glass pane named "&b空軍の研究ポイントを振込&6:1ポイント" with lore "" to run "facility %{_player}% transfer 1 1"
				format slot 5 of {_player} with light blue glass pane named "&b空軍の研究ポイントを振込&6:10ポイント" with lore "" to run "facility %{_player}% transfer 10 1"
				format slot 6 of {_player} with light blue glass pane named "&b空軍の研究ポイントを振込&6:全て" with lore "" to run "facility %{_player}% transfer -1 1"
			else if arg-3 is -1:
				set {_n} to 0
				set {_count} to 0
				loop 36 times:
					loop 64 times:
						if slot {_n} of {_player}'s inventory is {facility.pointitem.%arg-4%}:
							add 1 to {RP.%{_player}%.%arg-4%}
							remove {facility.pointitem.%arg-4%} from {_player}
							add 1 to {_count}
						else:
							exit loop
					add 1 to {_n}
				play door close at location of {_player} with pitch 2
				message "&6[Reserch]&b%{_count}%&3ポイント振り込みました" to {_player}

			else:
				set {_n} to 0
				set {_count} to 0
				loop 36 times:
					loop 64 times:
						if slot {_n} of {_player}'s inventory is {facility.pointitem.%arg-4%}:
							if {_count} >= arg-3:
								exit loop
							add 1 to {RP.%{_player}%.%arg-4%}
							remove {facility.pointitem.%arg-4%} from {_player}
							add 1 to {_count}
						else:
							exit loop
					add 1 to {_n}
				play door close at location of {_player} with pitch 2
				message "&6[Reserch]&b%{_count}%&3ポイント振り込みました" to {_player}
		#=============
		#   時間回復
		#=============
		if arg-2 is "time":
			set {_n} to 0
			loop 45 times:
				if {facility.ID.list::%{_n}%} is true:
					if {facility.%{_n}%::type} is "army" or "airforce":
						add {facility.%{_n}%::capacity.rate} to {facility.%{_n}%::capacity}
						#最大値を超えていたら最大値に
						if {facility.%{_n}%::capacity.max} < {facility.%{_n}%::capacity}:
							set {facility.%{_n}%::capacity} to {facility.%{_n}%::capacity.max}
				add 1 to {_n}

		if arg-2 is not set:
			execute console command "/facility %{_player}% facility_list"

		#リストを表示
		if arg-2 is "facility_list":
			#設定終了用
			if {facility.setting.type::%{_player}%} is "block":
				clear {facility.setting.type::%{_player}%}
				clear {facility.setting.data::%{_player}%}
				message "&6[Facility]&eインターフェースブロックの追加を終了しました" to {_player}

			wait 2 tick
			open chest with 6 row named "&d登録済み施設" to {_player}
			wait 1 tick
			set {_n} to 0
			set {_slot} to 0
			loop 45 times:
				if {facility.ID.list::%{_n}%} is true:
					#タイプにより説明を変更
					if {facility.%{_n}%::type} is "army" or "airforce":
						set {_lore} to "||&d最大容量:&6%{facility.%{_n}%::capacity.max}%||&d回復量:&6%{facility.%{_n}%::capacity.rate}%||&d引き出し量:&6%{facility.%{_n}%::output.max}%||&dクールタイム:&6%{facility.%{_n}%::output.rate}%"

						if {facility.%{_n}%::type} is "army":
							set {_type} to "||&dType:&6研究ポイント_陸軍"
						else:
							set {_type} to "||&dType:&6研究ポイント_空軍"

					else:
						set {_type} to "||&dType:&6アイテム交換"
						set {_p} to 0
						set {_txt} to "%{facility.%{_n}%::item.output.%{_n}%}%"
						loop 8 times:
							add 1 to {_p}
							if {facility.%{_n}%::item.output.%{_p}%} parsed as item is set:
								set {_txt} to "%{_txt}%&e,&3%{facility.%{_n}%::item.output.%{_p}%}%"
						set {_lore} to "||&d要求アイテム:&6%{facility.%{_n}%::item.request}%||&d出力アイテム:&6%{facility.%{_n}%::item.output}%||&dクールタイム:&6%{facility.%{_n}%::output.rate}%"

					format slot {_slot} of {_player} with beacon named "&dName:&f%{facility.%{_n}%::name}%" with lore "&dLore:&f%{facility.%{_n}%::lore}%%{_type}%%{_lore}%||&e施設の管理を開く" to close then run "facility %{_player}% facility_edit_open %{_n}%"
					add 1 to {_slot}

				add 1 to {_n}
		
			format slot 46 of {_player} with red glass pane named "&4稼働停止" with lore "" to close then run "facility %{_player}% stop"
			format slot 45 of {_player} with light green glass pane named "&2稼働開始" with lore "" to close then run "facility %{_player}% start"
			format slot 47 of {_player} with white glass pane named "&6稼働する施設の選択" with lore "" to close then run "facility %{_player}% operation_all_info"

			format slot 49 of {_player} with red glass pane named "&4ローテーション停止" with lore "" to close then run "facility %{_player}% rotate_stop"
			format slot 48 of {_player} with light green glass pane named "&2ローテーションを進める" with lore "" to close then run "facility %{_player}% rotate_next"
			format slot 50 of {_player} with white glass pane named "&6稼働する施設の選択" with lore "" to close then run "facility %{_player}% operation_rote_info"

			format slot 51 of {_player} with sign item named "&a陸軍&3研究ポイントのアイテムを設定" with lore "&61陸軍研究ポイント:&e%{facility.pointitem.0}%" to close then run "facility %{_player}% set_pointitem 0"
			format slot 52 of {_player} with sign item named "&b空軍&3研究ポイントのアイテムを設定" with lore "&61空軍研究ポイント:&e%{facility.pointitem.1}%" to close then run "facility %{_player}% set_pointitem 1"

			format slot 53 of {_player} with paper named "&d新規追加" with lore "&6施設を新規追加する" to close then run "facility %{_player}% facility_add"

		#=============
		#   稼働管理
		#=============
		#全体稼働の内容編集
		if arg-2 is "operation_all_info":
			wait 2 tick
			open chest with 6 row named "&a全体稼働に含める施設" to {_player}
			wait 1 tick
			set {_n} to 0
			loop 45 times:
				if {facility.ID.list::%{_n}%} is true:
					if {facility.operation_all::%{_n}%} is set:
						format slot {_n} of {_player} with light green glass pane named "&dName:&f%{facility.%{_n}%::name}%" with lore "&dLore:&f%{facility.%{_n}%::lore}%||&c稼働リストから外す" to close then run "facility %{_player}% operation_all_clear %{_n}%"
					else:
						format slot {_n} of {_player} with black glass pane named "&dName:&f%{facility.%{_n}%::name}%" with lore "&dLore:&f%{facility.%{_n}%::lore}%||&b稼働リストに入れる" to close then run "facility %{_player}% operation_all_set %{_n}%"
					add 1 to {_n}

		if arg-2 is "operation_all_clear":
			clear {facility.operation_all::%arg-3%}
			execute console command "/facility %{_player}% operation_all_info"

		if arg-2 is "operation_all_set":
			set {facility.operation_all::%arg-3%} to arg-3
			execute console command "/facility %{_player}% operation_all_info"

		#ローテーション稼働管理
		if arg-2 is "operation_rote_info":
			wait 2 tick
			open chest with 6 row named "&1ローテーション稼働に含める施設" to {_player}
			wait 1 tick
			set {_n} to 0
			loop 9 times:
				set {_slot} to {_n}
				loop 4 times:
					format slot {_slot} of {_player} with light gray glass pane named "&6No.%{_n}%に追加する施設を選択" with lore "" to close then run "facility %{_player}% operation_rote_add %{_n}%"
					add 9 to {_slot}
				add 1 to {_n}	

			set {_n} to 0
			loop 9 times:
				if {facility.operation_rotate.%{_n}%::*} is set:
					set {_slot} to {_n}
					loop {facility.operation_rotate.%{_n}%::*}:
						format slot {_slot} of {_player} with light green glass pane named "&dName:&f%{facility.%loop-value-2%::name}%" with lore "&dLore:&f%{facility.%loop-value-2%::lore}%||&c稼働リストから外す" to close then run "facility %{_player}% operation_rote_remove %{_n}% %loop-value-2%"
						add 9 to {_slot}
				add 1 to {_n}

		if arg-2 is "operation_rote_add":
			if arg-4 is not set:
				wait 2 tick
				open chest with 6 row named "&1ローテーションNo.&2%arg-3%&1に追加する施設を選択" to {_player}
				wait 1 tick
				set {_n} to 0
				loop 45 times:
					if {facility.ID.list::%{_n}%} is true:
						if {facility.operation_rotate.%arg-3%::%arg-3%} is not set:
							format slot {_n} of {_player} with light green glass pane named "&dName:&f%{facility.%{_n}%::name}%" with lore "&dLore:&f%{facility.%{_n}%::lore}%||&b稼働リストに入れる" to close then run "facility %{_player}% operation_rote_add %arg-3% %{_n}%"
					add 1 to {_n}
			else:
				set {facility.operation_rotate.%arg-3%::%arg-4%} to arg-4
				execute console command "/facility %{_player}% operation_rote_info"

		if arg-2 is "operation_rote_remove":
			clear {facility.operation_rotate.%arg-3%::%arg-4%}
			execute console command "/facility %{_player}% operation_rote_info"

		#=============
		#   施設管理
		#=============
		#新規追加処理
		if arg-2 is "facility_add":
			set {_n} to 0
			loop 45 times:
				if {facility.ID.list::%{_n}%} is not set:
					set {facility.ID.list::%{_n}%} to true
					#初期値
					set {facility.%{_n}%::name} to "未設定"
					set {facility.%{_n}%::lore} to "未設定"
					set {facility.%{_n}%::type} to "factory"

					set {facility.%{_n}%::capacity.max} to 500
					set {facility.%{_n}%::capacity.rate} to 20
					set {facility.%{_n}%::capacity} to 0
					set {facility.%{_n}%::output.max} to 20

					set {facility.%{_n}%::item.output} to air
					set {facility.%{_n}%::item.request} to air

					set {facility.%{_n}%::output.rate} to 5

					message "&6[Facility]&d施設を新規追加しました" to {_player}
					execute console command "/facility %{_player}% facility_list"
					stop
				add 1 to {_n}
			message "&6[Facility]&4施設が最大数に達しています" to {_player}

		#削除処理
		if arg-2 is "facility_remove":
			message "&6[Facility]&4施設&cID:%arg-3%,Name:%{facility.%arg-3%::name}%&4を削除しました" to {_player}
			clear {facility.ID.list::%arg-3%}
			clear {facility.%arg-3%::*}
			loop {facility.sign.%arg-3%::*}:
				clear {facility.interface.%loop-value%}
			loop {facility.block.%arg-3%::*}:
				clear {facility.interface.%loop-value%}
			clear {facility.sign.%arg-3%::*}
			clear {facility.block.%arg-3%::*}

		#施設の内容編集
		if arg-2 is "facility_edit_open":
			wait 2 tick
			open chest with 3 row named "&1施設の設定 &dID:&3%arg-3%" to {_player}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"
		#書き込み
		if arg-2 is "facility_edit_write":
			wait 1 tick
			#表示用看板
			format slot 0 of {_player} with sign item named "&d表示用看板の追加" with lore "" to close then run "facility %{_player}% sign_add %arg-3%"
			format slot 1 of {_player} with sign item named "&d登録された看板を削除" with lore "" to run "facility %{_player}% sign_clear %arg-3%"
			#クリックするインターフェース
			format slot 2 of {_player} with fancy brick named "&dインターフェースブロックの追加" with lore "" to close then run "facility %{_player}% block_add %arg-3%"
			format slot 3 of {_player} with fancy brick named "&dインターフェースの登録を削除" with lore "" to run "facility %{_player}% block_clear %arg-3%"
			#施設のType
			if {facility.%arg-3%::type} is "army":
				set slot 4 of {_player}'s open inventory to air
				format slot 5 of {_player} with book and quill named "&6航空機工場&eに設定" with lore "" to run "facility %{_player}% type_airforce %arg-3%"
				format slot 6 of {_player} with workbench named "&6戦略物資工場&eに設定" with lore "" to run "facility %{_player}% type_factory %arg-3%"
			else if {facility.%arg-3%::type} is "airforce":
				format slot 4 of {_player} with book and quill named "&6戦車工場&eに設定" with lore "" to run "facility %{_player}% type_army %arg-3%"
				set slot 5 of {_player}'s open inventory to air
				format slot 6 of {_player} with workbench named "&6戦略物資工場&eに設定" with lore "" to run "facility %{_player}% type_factory %arg-3%"
			else:
				format slot 4 of {_player} with book and quill named "&6戦車工場&eに設定" with lore "" to run "facility %{_player}% type_army %arg-3%"
				format slot 5 of {_player} with book and quill named "&6航空機工場&eに設定" with lore "" to run "facility %{_player}% type_airforce %arg-3%"
				set slot 6 of {_player}'s open inventory to air

			#名前|説明文
			format slot 7 of {_player} with name tag named "&d名前を設定" with lore "&6現在の名称:&e%{facility.%arg-3%::name}%" to close then run "facility %{_player}% name_set %arg-3%"
			format slot 8 of {_player} with name tag named "&d説明を設定" with lore "&6現在の説明:&e%{facility.%arg-3%::lore}%" to close then run "facility %{_player}% lore_set %arg-3%"

			#数値設定用
			#Typeにより変更
			if {facility.%arg-3%::type} is "army" or "airforce":
				#最大容量
				format slot 9 of {_player} with light green glass pane named "&a最大容量の値を&61000&2追加" with lore "&6最大容量:&e%{facility.%arg-3%::capacity.max}%" to run "facility %{_player}% capacity.max %arg-3% 1000"
				format slot 10 of {_player} with light green glass pane named "&a最大容量の値を&6100&2追加" with lore "&6最大容量:&e%{facility.%arg-3%::capacity.max}%" to run "facility %{_player}% capacity.max %arg-3% 100"
				format slot 18 of {_player} with blue glass pane named "&c最大容量の値を&61000&4減少" with lore "&6最大容量:&e%{facility.%arg-3%::capacity.max}%" to run "facility %{_player}% capacity.max %arg-3% -1000"
				format slot 19 of {_player} with blue glass pane named "&c最大容量の値を&6100&4減少" with lore "&6最大容量:&e%{facility.%arg-3%::capacity.max}%" to run "facility %{_player}% capacity.max %arg-3% -100"
				#時間当たりの回復
				format slot 11 of {_player} with light green glass pane named "&a回復量を&610&2追加" with lore "&6回復量:&e%{facility.%arg-3%::capacity.rate}%" to run "facility %{_player}% capacity.rate %arg-3% 10"
				format slot 12 of {_player} with light green glass pane named "&a回復量を&61&2追加" with lore "&6回復量:&e%{facility.%arg-3%::capacity.rate}%" to run "facility %{_player}% capacity.rate %arg-3% 1"
				format slot 20 of {_player} with blue glass pane named "&c回復量を&610&4減少" with lore "&6回復量:&e%{facility.%arg-3%::capacity.rate}%" to run "facility %{_player}% capacity.rate %arg-3% -10"
				format slot 21 of {_player} with blue glass pane named "&c回復量を&61&4減少" with lore "&6回復量:&e%{facility.%arg-3%::capacity.rate}%" to run "facility %{_player}% capacity.rate %arg-3% -1"
				#1回あたりの引き出し量
				format slot 13 of {_player} with yellow glass pane named "&a引き出し量を&610&2追加" with lore "&6引き出し量:&e%{facility.%arg-3%::output.max}%" to run "facility %{_player}% output.max %arg-3% 10"
				format slot 14 of {_player} with yellow glass pane named "&a引き出し量を&61&2追加" with lore "&6引き出し量:&e%{facility.%arg-3%::output.max}%" to run "facility %{_player}% output.max %arg-3% 1"
				format slot 22 of {_player} with red glass pane named "&c引き出し量を&610&4減少" with lore "&6引き出し量:&e%{facility.%arg-3%::output.max}%" to run "facility %{_player}% output.max %arg-3% -10"
				format slot 23 of {_player} with red glass pane named "&c引き出し量を&61&4減少" with lore "&6引き出し量:&e%{facility.%arg-3%::output.max}%" to run "facility %{_player}% output.max %arg-3% -1"
				#クールタイム
				format slot 15 of {_player} with yellow glass pane named "&aクールタイムを&610秒&2追加" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% 10"
				format slot 16 of {_player} with yellow glass pane named "&aクールタイムを&61秒&2追加" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% 1"
				format slot 24 of {_player} with red glass pane named "&cクールタイムを&610秒&4減少" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% -10"
				format slot 25 of {_player} with red glass pane named "&cクールタイムを&61秒&4減少" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% -1"
			else:
				#使わない部分を消す
				set {_slot} to 13
				loop 2 times:
					loop 4 times:
						set slot {_slot} of {_player}'s open inventory to air
						add 1 to {_slot}
					add 5 to {_slot}

				#要求アイテム
				format slot 9 of {_player} with hopper named "&3要求アイテムを設定" with lore "&6要求アイテム:&e%{facility.%arg-3%::item.request}%||&3インベントリの左端のアイテムを要求アイテムに登録" to run "facility %{_player}% set_request %arg-3%"
				#出力アイテム
				set {_n} to 0
				set {_txt} to "&6出力アイテム:&e%{facility.%arg-3%::item.output.%{_n}%}%"
				loop 8 times:
					add 1 to {_n}
					set {_txt} to "%{_txt}%||&6出力アイテム:&e%{facility.%arg-3%::item.output.%{_n}%}%"

				format slot 10 of {_player} with dispenser named "&3出力アイテムを設定" with lore "%{_txt}%||&3インベントリのホットバーのアイテムを出力アイテムに登録" to run "facility %{_player}% set_output %arg-3%"
				#クールタイム
				format slot 11 of {_player} with light green glass pane named "&aクールタイムを&610秒&2追加" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% 10"
				format slot 12 of {_player} with light green glass pane named "&aクールタイムを&61秒&2追加" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% 1"
				format slot 20 of {_player} with blue glass pane named "&cクールタイムを&610秒&4減少" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% -10"
				format slot 21 of {_player} with blue glass pane named "&cクールタイムを&61秒&4減少" with lore "&6クールタイム:&e%{facility.%arg-3%::output.rate}%秒" to run "facility %{_player}% output.rate %arg-3% -1"

				if {facility.%arg-3%::ct.private} is true:
					format slot 18 of {_player} with grass block named "&3クールタイムを全体に" with lore "&6クールタイムType:&ePrivate" to run "facility %{_player}% ct_public %arg-3%"
				else:
					format slot 18 of {_player} with player head named "&3クールタイムを個人ごとに" with lore "&6クールタイムType:&ePublic" to run "facility %{_player}% ct_private %arg-3%"

				set slot 19 of {_player}'s open inventory to air

			#戻る
			format slot 26 of {_player} with wood door item named "&dリストに戻る" with lore "" to close then run "facility %{_player}% facility_list"

			#削除
			format slot 17 of {_player} with lava bucket named "&4施設を削除" with lore "" to close then run "facility %{_player}% facility_remove %arg-3%"

		#=============
		# クールタイムType
		#=============
		if arg-2 is "ct_public":
			clear {facility.%arg-3%::ct.private}
			message "&6[Facility]&eクールタイムTypeを&3Public&eに設定しました" to {_player}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		if arg-2 is "ct_private":
			set {facility.%arg-3%::ct.private} to true
			message "&6[Facility]&eクールタイムTypeを&3Private&eに設定しました" to {_player}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		#=============
		#  アイテム設定
		#=============
		if arg-2 is "set_request":
			set {facility.%arg-3%::item.request} to "%slot 0 of {_player}'s inventory%"
			message "&6[Facility]&e要求アイテムに&3%{facility.%arg-3%::item.request}%&eを登録しました" to {_player}	
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		if arg-2 is "set_output":
			set {_n} to 0
			loop 9 times:
				set {facility.%arg-3%::item.output.%{_n}%} to "%slot {_n} of {_player}'s inventory%"
				message "&6[Facility]&e出力アイテムに&3%{facility.%arg-3%::item.output.%{_n}%}%&eを登録しました" to {_player}
				add 1 to {_n}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"


		if arg-2 is "set_pointitem":
			wait 1 tick
			set {facility.pointitem.%arg-3%} to "%slot 0 of {_player}'s inventory%"
			message "&6[Facility]&e研究ポイントに&3%{facility.pointitem.%arg-3%}%&eを設定しました" to {_player}
			execute console command "/facility %{_player}% facility_list"

		#=============
		#  数値変更
		#=============
		if arg-2 is "capacity.max" or "capacity.rate" or "output.max" or "output.rate":
			add arg-4 to {facility.%arg-3%::%arg-2%}
			#最大値を超えないように
			if arg-2 is "capacity.max":
				if {facility.%arg-3%::capacity.max} < {facility.%arg-3%::capacity}:
					set {facility.%arg-3%::capacity} to {facility.%arg-3%::capacity.max}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		#=============
		#   名前変更
		#=============
		if arg-2 is "name_set":
			set {facility.setting.type::%{_player}%} to "name"
			set {facility.setting.data::%{_player}%} to arg-3
			message "&6[Facility]&e新しい名前をチャット欄に入力してください" to {_player}

		#=============
		#   説明変更
		#=============
		if arg-2 is "lore_set":
			set {facility.setting.type::%{_player}%} to "lore"
			set {facility.setting.data::%{_player}%} to arg-3
			message "&6[Facility]&e新しい説明ををチャット欄に入力してください" to {_player}

		#=============
		#   Type変更
		#=============
		if arg-2 is "type_army":
			set {facility.%arg-3%::type} to "army"
			message "&6[Facility]&d施設のTypeを&5戦車工場&dに変更しました" to {_player}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		if arg-2 is "type_airforce":
			set {facility.%arg-3%::type} to "airforce"
			message "&6[Facility]&d施設のTypeを&5航空機工場&dに変更しました" to {_player}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		if arg-2 is "type_factory":
			set {facility.%arg-3%::type} to "factory"
			message "&6[Facility]&d施設のTypeを&5戦略物資工場&dに変更しました" to {_player}
			execute console command "/facility %{_player}% facility_edit_write %arg-3%"

		#=============
		#   看板関連
		#=============
		#追加
		if arg-2 is "sign_add":
			set {facility.setting.type::%{_player}%} to "sign"
			set {facility.setting.data::%{_player}%} to arg-3

			message "&6[Facility]&3追加する看板を右クリックしてください" to {_player}
			message "&6[Facility]&3看板以外を右クリックで設定を終了します" to {_player}

		#削除
		if arg-2 is "sign_clear":
			loop {facility.sign.%arg-3%::*}:
				set line 1 of loop-value to ""
				set line 2 of loop-value to ""
				set line 3 of loop-value to ""
				set line 4 of loop-value to ""
				clear {facility.interface.%loop-value%}
			clear {facility.sign.%arg-3%::*}
			message "&6[Facility]&4看板の登録を削除しました" to {_player}

		#=======================
		#  インターフェースブロック関連
		#=======================
		#追加
		if arg-2 is "block_add":
			set {facility.setting.type::%{_player}%} to "block"
			set {facility.setting.data::%{_player}%} to arg-3

			message "&6[Facility]&3インターフェースにするブロックを右クリックしてください" to {_player}
			message "&6[Facility]&3GUIを開いて設定を終了します" to {_player}

		#削除
		if arg-2 is "block_clear":
			loop {facility.block.%arg-3%::*}:
				clear {facility.interface.%loop-value%}
			clear {facility.block.%arg-3%::*}
			message "&6[Facility]&4インターフェースブロックの登録を削除しました" to {_player}



on rightclick:
	#看板追加処理
	if {facility.setting.type::%player%} is "sign":
		cancel event
		if event-block is sign:
			#重複チェック
			loop {facility.sign.%{facility.setting.data::%player%}%::*}:
				if loop-value is block:
					message "&6[Facility]&c既に登録済みの看板です"
					stop
			add block to {facility.sign.%{facility.setting.data::%player%}%::*}
			set {facility.interface.%block%} to {facility.setting.data::%player%}
			
			set line 1 to "[Facility]"
			set line 2 to "%{facility.%{facility.setting.data::%player%}%::name}%"
			if {facility.operation} is true:
				set line 3 to "[&a稼働中&0]"
			else:
				set line 3 to "[&4停止中&0]"
			message "&6[Facility]&a看板を追加しました"
			stop
		else:
			clear {facility.setting.type::%player%}
			clear {facility.setting.data::%player%}
			message "&6[Facility]&e設定を終了しました"
			stop

	#インターフェース追加処理
	if {facility.setting.type::%player%} is "block":
		cancel event
		#重複チェック
		loop {facility.block.%{facility.setting.data::%player%}%::*}:
			if loop-value is block:
				message "&6[Facility]&c既に登録済みのブロックです"
				stop

		add block to {facility.block.%{facility.setting.data::%player%}%::*}
		set {facility.interface.%block%} to {facility.setting.data::%player%}
			
		message "&6[Facility]&aインターフェースブロックを追加しました"
		stop

	#----------------
	#  施設の稼働処理
	#----------------
	if {facility.interface.%block%} is set:
		#ブロックがなければ止める
		if block is not set:
			stop

		set {_n} to {facility.interface.%block%}

		#稼働しているか
		if {facility.operation::%{_n}%} is not set:
			message "&6[Facility]&c現在稼働していません"
			stop

		#factory
		if {facility.%{_n}%::type} is "factory":
			#クールタイムType
			if {facility.%{_n}%::ct.private} is true:
				set {_ct} to player
			else:
				set {_ct} to "public"

			#CT処理
			if {facility.ct.factory.%{_n}%::%{_ct}%} is set:
				message "&6[Facility]&eクールタイム:&3%{facility.ct.factory.%{_n}%::%{_ct}%}%秒"
			else:
				#要求があるかどうか
				if {facility.%{_n}%::item.request} parsed as item is set:
					if player is not holding {facility.%{_n}%::item.request} parsed as item:
						message "&6[Facility]&4%{facility.%{_n}%::item.request}%&cで右クリックしてください"
						stop
					remove {facility.%{_n}%::item.request} parsed as item from player
				set {facility.ct.factory.%{_n}%::%{_ct}%} to {facility.%{_n}%::output.rate}

				set {_p} to 0
				loop 9 times:
					if {facility.%{_n}%::item.output.%{_p}%}  parsed as item is set:
						if player has enough space for {facility.%{_n}%::item.output.%{_p}%} parsed as item:
							give {facility.%{_n}%::item.output.%{_p}%} parsed as item to player
						else:
							drop {facility.%{_n}%::item.output.%{_p}%} parsed as item at player
						message "&6[Facility]&b%{facility.%{_n}%::item.output.%{_p}%} parsed as item%&aを受け取りました"
					add 1 to {_p}

				loop {facility.%{_n}%::output.rate} times:
					wait 20 tick
					remove 1 from {facility.ct.factory.%{_n}%::%{_ct}%}
				clear {facility.ct.factory.%{_n}%::%{_ct}%}
					
		#army
		if {facility.%{_n}%::type} is "army" or "airforce":
			if {facility.%{_n}%::type} is "army":
				set {_type} to 0
				set {_type.name} to "&2陸軍"
			else:
				set {_type} to 1
				set {_type.name} to "&b空軍"

			set {_ct} to player

			#CT処理
			if {facility.ct.factory.%{_n}%::%{_ct}%} is set:
				message "&6[Facility]&eクールタイム:&3%{facility.ct.factory.%{_n}%::%{_ct}%}%秒"
			else:
				
				if {facility.%{_n}%::capacity} < {facility.%{_n}%::output.max}:
					message "&6[Facility]&c施設の研究ポイントが尽きました &3シフト＋左クリックで施設の詳細なステータスが見られます"
					play lava pop at location of player with pitch 0.3
					stop
				set {facility.ct.factory.%{_n}%::%{_ct}%} to {facility.%{_n}%::output.rate}

				remove {facility.%{_n}%::output.max} from {facility.%{_n}%::capacity}

				message "&6[Facility]%{_type.name}%&aの研究ポイント&b%round {facility.%{_n}%::output.max}/10*3%&aが振り込まれました"
				#振込処理
				add round {facility.%{_n}%::output.max}/10*3 to {RP.%player%.%{_type}%}
				if {ch-countryid.%player%} is set:
					add round {facility.%{_n}%::output.max}/10*1 to {RP.%{ch-countryid.%player%}%.%{_type}%}


				message "&6[Facility]%{_type.name}%&aの研究ポイント&b%round {facility.%{_n}%::output.max}/10*7%&eを受け取りました"


				loop round {facility.%{_n}%::output.max}/10*7 times:
					if player has enough space for {facility.pointitem}:
						give {facility.pointitem.%{_type}%} to player
					else:
						drop {facility.pointitem.%{_type}%} at player

				play door open at location of player with pitch 1.5
				wait 1 tick
				play level up at location of player with pitch 1.5

				loop {facility.%{_n}%::output.rate} times:
					wait 20 tick
					remove 1 from {facility.ct.factory.%{_n}%::%{_ct}%}
				clear {facility.ct.factory.%{_n}%::%{_ct}%}


#デフラグ
on join:
	clear {facility.ct.%player%}

on skript load:
	set {_n} to 0
	loop 45 times:
		clear {facility.ct.factory.%{_n}%::*}
		add 1 to {_n}


#詳細表示
on leftclick:
	if {facility.interface.%block%} is set:
		if block is not set:
			stop
		cancel event
		#ct
		if {facility.ct.%player%} is not set:
			set {facility.ct.%player%} to true

			if {facility.operation::%{facility.interface.%block%}%} is true:
				message "&6[Facility] &a稼働状況:&a&l稼働中"
			else:
				message "&6[Facility] &a稼働状況:&4&l停止中"

			message "&6[Facility] &e施設名:&b&l%{facility.%{facility.interface.%block%}%::name}%"
			message "&6[Facility] &e説明:&3%{facility.%{facility.interface.%block%}%::lore}%"

			#スニークしていたら
			if player is sneaking:
				message "&6[Facility] &a施設の詳細なステータス"

				if {facility.%{facility.interface.%block%}%::type} is "army" or "airforce":
					if {facility.%{facility.interface.%block%}%::type} is "army":
						message "&6[Facility] &e施設Type:&5戦車工場"
					else:
						message "&6[Facility] &e施設Type:&5航空機工場"

					message "&6[Facility] &e現在容量:&3%{facility.%{facility.interface.%block%}%::capacity}%/%{facility.%{facility.interface.%block%}%::capacity.max}% &e1分当たりの回復量:&3%{facility.%{facility.interface.%block%}%::capacity.rate}%"
					message "&6[Facility] &e1回の獲得量:&3%{facility.%{facility.interface.%block%}%::output.max}% &eクールタイム:&3%{facility.%{facility.interface.%block%}%::output.rate}%秒"
				else:
					message "&6[Facility] &e施設Type:&5戦略物資工場"
					set {_n} to 0
					set {_txt} to "%{facility.%{facility.interface.%block%}%::item.output.%{_n}%}%"
					loop 8 times:
						add 1 to {_n}
						if {facility.%{facility.interface.%block%}%::item.output.%{_n}%} parsed as item is set:
							set {_txt} to "%{_txt}%&e,&3%{facility.%{facility.interface.%block%}%::item.output.%{_n}%}%"
					message "&6[Facility] &e要求アイテム:&3%{facility.%{facility.interface.%block%}%::item.request}% &e出力アイテム:&3%{_txt}%"
					message "&6[Facility] &eクールタイム:&3%{facility.%{facility.interface.%block%}%::output.rate}%秒"

			wait 5 tick
			clear {facility.ct.%player%}


#文字列取得
on chat:
	if {facility.setting.type::%player%} is "name":
		cancel event
		set {facility.%{facility.setting.data::%player%}%::name} to message
		message "&6[Facility]&a施設ID&2%{facility.setting.data::%player%}%&aの名前を%message%に設定しました"
		execute console command "/facility %player% facility_edit_open %{facility.setting.data::%player%}%"
		#看板書き換え
		loop {facility.sign.%{facility.setting.data::%player%}%::*}:
			set line 2 of loop-value to message
		clear {facility.setting.type::%player%}
		clear {facility.setting.data::%player%}

	if {facility.setting.type::%player%} is "lore":
		cancel event
		set {facility.%{facility.setting.data::%player%}%::lore} to message
		message "&6[Facility]&a施設ID&2%{facility.setting.data::%player%}%&aの説明を%message%に設定しました"
		execute console command "/facility %player% facility_edit_open %{facility.setting.data::%player%}%"
		clear {facility.setting.type::%player%}
		clear {facility.setting.data::%player%}

on break:
	if {facility.interface.%block%} is set:
		cancel event